buildscript {
	repositories {
		ivy {
			url "http://dev/ivyrep/public/"
			layout 'pattern' , {
				artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
				ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
			}
		}
	}

	dependencies {
		classpath group: 'eu.ydp', name: 'gradleGwtCompilerPlugin', version: 'latest.integration'
	}
}

import eu.ydp.gradle.gwt.GWTCompileTask

def empiriaWar = file( "war")
def empiriaBuildDir = new File( empiriaWar, "empiria.player" )
def empiriaWebkitWar = file( "build/gwt")
def empiriaWebkitBuildDir = new File( empiriaWebkitWar, "empiria.player" )
def versionFile= new File("${projectDir}${File.separator}src${File.separator}eu${File.separator}ydp${File.separator}empiria${File.separator}player${File.separator}client${File.separator}version${File.separator}version.txt")
def buildProfile = project.getProperties().get('buildProfile', 'develop')

println "Picked build profile [$buildProfile]"

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'sonar-runner'
apply from: "gradle/profiles/${buildProfile}.gradle"
apply plugin: 'ivy-publish'

sourceCompatibility = 1.6
compileJava.options.encoding = 'UTF-8'

ext {
    tstamp=new Date().format( "yyyyMMddHHmmssSSS")
    buildnumber="${System.env["BUILD_NUMBER"]?:"${tstamp}"}"
    jacocoReport = "${buildDir}/jacoco/testJUnit.exec"
}	
	
repositories {
    ivy {
        url "http://dev/ivyrep/public/"
        layout 'pattern' , {
            artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
        }
    }
    ivy {
        url "http://dev/ivyrep/shared/"
        layout 'pattern' , {
            artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
        }
    }
    mavenCentral()
    maven {
        url 'http://gwtquery-plugins.googlecode.com/svn/mavenrepo/'
    }
}

configurations.all {
    resolutionStrategy {
        force group: 'eu.ydp', name: 'ydpgwtutils', version: "$ydp_gwt_utils_ver"
    }
}

dependencies {
    compile group: 'com.google.gwt', name: 'gwt-servlet', version: '2.5.1'
    compile group: 'com.google.gwt', name: 'gwt-user', version: '2.5.1'
    compile group: 'com.google.gwt', name: 'gwt-dev', version: '2.5.1'

    compile group: 'com.google.gwt.inject', name: 'gin', version: '2.0.0'
    compile group: 'com.googlecode.gwtquery', name: 'gwtquery', version: '1.3.3'
    compile group: 'com.googlecode.gwtquery.bundles', name: 'gquery-dnd-bundle', version: '1.0.6'

    compile group: 'eu.ydp', name: 'mathPlayer', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'canvasadapter', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'jsfilerequest', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'gwtFlashMedia', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'gwtCreateJs', version: gwt_createjs_ver
    compile group: 'eu.ydp', name: 'jaxb4gwt', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'matheclipse-parser-gwt', version: '0.0.10+'

    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.9.5'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.0.1'
    testCompile group: 'org.easytesting', name: 'fest-assert-core', version: '2.0M10'
    testCompile group: 'xmlunit', name: 'xmlunit', version: '1.3'
    testCompile group: 'org.reflections', name: 'reflections', version: '0.9.9-RC1'
}

eclipse {
    project {
        natures 'com.google.gwt.eclipse.core.gwtNature', 'org.springsource.ide.eclipse.gradle.core.nature', 'org.eclipse.jdt.groovy.core.groovyNature'
        buildCommand 'com.google.gdt.eclipse.core.webAppProjectValidator'
        buildCommand 'com.google.gwt.eclipse.core.gwtProjectValidator'
    }
    classpath {
        containers 'com.google.gwt.eclipse.core.GWT_CONTAINER'
		
        defaultOutputDir file('/war/WEB-INF/classes')
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", sonar_host_url
        property "sonar.jdbc.url", sonar_jdbc_url
        property "sonar.jdbc.driverClassName", sonar_jdbc_driverClassName
        property "sonar.jdbc.username", sonar_jdbc_username
        property "sonar.jdbc.password", sonar_jdbc_password
        property "sonar.projectName",  sonar_project_name
        property "sonar.core.codeCoveragePlugin", "jacoco"
    }
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
        resources {
            srcDir 'src/'
        }
    }
    test {
        java {
            srcDir 'test/'
        }
        resources {
            srcDir 'test/'
        }
    }
    
    sourceSets.test.runtimeClasspath += files(sourceSets.main.java.srcDirs)
    sourceSets.test.runtimeClasspath += files(sourceSets.test.java.srcDirs)
}

task testJUnit( type: Test ) {
    jacoco {
        enabled = true
    }
    useJUnit()
    include "**/JUnitTestSuite.*"

    forkEvery = 1
    maxHeapSize = '512M'
    jvmArgs( ['-XX:MaxPermSize=256M', '-Xss1M']  )
}
task testGWTUnit( type: Test ) {
    jacoco {
        enabled = false
    }
    useJUnit()
    include "**/GWTTestCaseSuite.*"
    include "**/MainFlowProcessorTest.*"

    forkEvery = 1
    maxHeapSize = '512M'
    jvmArgs( ['-XX:MaxPermSize=256M', '-Xss1M']  )

}

task test( overwrite: true, dependsOn: [testJUnit,testGWTUnit] ) {
    ext {
        // this is needed for sonarRunner
        reports = [junitXml: [destination: file("${buildDir}/test-results")]]
        jacoco = [destinationFile: file(jacocoReport)]
    }
}

jacocoTestReport {
    executionData = files(jacocoReport)
    reports {
        xml.enabled true
        csv.enabled false
    }
}
testJUnit.finalizedBy jacocoTestReport

task cleanCache(dependsOn: clean) {
    delete "gwt-unitCache/"
}

task compileMain(dependsOn: [classes], type: GWTCompileTask) {
    module = 'eu.ydp.empiria.player.Player'
    buildDir = empiriaWar
}

task compileDev(dependsOn: [classes], type: GWTCompileTask) {
	module = 'eu.ydp.empiria.player.Player_dev'
	buildDir = empiriaWar
}

task compileWebkit(dependsOn: [classes], type: GWTCompileTask) {
    module = 'eu.ydp.empiria.player.Player_webkit'
    buildDir = empiriaWebkitWar
}


def configCopyMetaInfoAndZip( playerDir, targetZip ) {
    return {
        def metadataProperties = [version: version, tstamp: tstamp, buildnumber: buildnumber]

        inputs.properties( metadataProperties )
        inputs.file file("metadata.template.xml")

        doFirst {
            copy {
                from file("metadata.template.xml")
                into playerDir
                rename ".*", "metadata.xml"
                expand( metadataProperties )
            }
        }

        archiveName = targetZip
        destinationDir = file( "dist" )
        include( [ "**/*"] )
        from playerDir
    }
}

task createVersion(){
	versionFile.createNewFile()
	versionFile.write "${project.ext.version}-${buildnumber}"
}

task zipMain( dependsOn: [compileMain], type: Zip ) {
    configure configCopyMetaInfoAndZip( empiriaBuildDir, "empiriaplayer.zip")
}
task zipWebkit( dependsOn: [compileWebkit], type: Zip ) {
    configure configCopyMetaInfoAndZip( empiriaWebkitBuildDir, "empiriaplayer_webkit.zip")
}

task compileAll( dependsOn: [compileMain, compileWebkit] )

task distMain( dependsOn: [cleanCache, createVersion, test, zipMain] )
task distWebkit( dependsOn: [cleanCache, createVersion, test, zipWebkit] )

task dist( dependsOn: [distMain, distWebkit] )

task wrapper(type: Wrapper) {
	gradleVersion = '1.10'
}

tasks.withType(JavaCompile) {
    options.compilerArgs = [ "-implicit:none" ]
    options.compilerArgs += ["-sourcepath", ""]
}

publishing {
    publications {
        ivy(IvyPublication) {
            organisation 'eu.ydp'
            module 'empiriaPlayer'
            revision "${project.ext.version}-${buildnumber}"
            descriptor.status = project.ext.status
			artifacts {
	            artifact("${projectDir}${File.separator}dist${File.separator}empiriaplayer.zip") {
	            	name 'empiriaplayer'
	            	classifier "source"
		            extension "zip"
		         	builtBy distMain	         	
	       		}
	       		
	       		artifact("${projectDir}${File.separator}dist${File.separator}empiriaplayer_webkit.zip") {
	            	name 'empiriaplayer_webkit'
	            	classifier "source"
		            extension "zip"
		         	builtBy distWebkit	         	
	       		}       	
       		}	
        }
    }
    repositories {
        ivy {
            url 'http://dev/ivyrep/public'
            layout 'pattern' , {
                artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
                ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            }
        }
    }
}
