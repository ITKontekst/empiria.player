import org.apache.ivy.core.settings.IvySettings

apply plugin: 'java'
sourceCompatibility = '1.5'

File userHome = new File( System.getProperty("user.home") );

IvySettings ivySettings = new IvySettings();
ivySettings.load(new File(userHome, ".ivy2/ivysettings.xml") );

repositories {
    ivy {
        url "http://dev/ivyrep/public/"
        layout 'pattern' , {
            artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
        }
    }
    ivy {
        url "http://dev/ivyrep/shared/"
        layout 'pattern' , {
            artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
        }
    }
    mavenCentral()
    maven {
        url 'http://gwtquery-plugins.googlecode.com/svn/mavenrepo/'
    }
}
dependencies {
    compile 'com.google.gwt:gwt-servlet:2.5.0'
    compile 'com.google.gwt:gwt-user:2.5.0'
    compile 'com.google.gwt:gwt-dev:2.5.0'

    compile 'com.google.gwt.inject:gin:2.0.0'
    compile 'com.googlecode.gwtquery:gwtquery:1.3.2'
    compile 'com.googlecode.gwtquery.bundles:gquery-dnd-bundle:1.0.6'

    compile 'eu.ydp:mathPlayer:latest.integration'
    compile 'eu.ydp:canvasadapter:latest.integration'
    compile 'eu.ydp:jsfilerequest:latest.integration'
    compile 'eu.ydp:ydpgwtutils:[1.11,1.12['
    compile 'eu.ydp:gwtFlashMedia:latest.integration'
    compile 'eu.ydp:gwtCreateJs:latest.integration'
    compile 'eu.ydp:jaxb4gwt:latest.integration'
    compile 'eu.ydp:matheclipse-parser-gwt:0.0.10+'

    testCompile 'org.hamcrest:hamcrest-library:1.3'
    testCompile 'org.mockito:mockito-core:1.9.5'
    testCompile 'junit:junit:4.11'
    testCompile 'pl.pragmatists:JUnitParams:1.0.1'
    testCompile 'org.easytesting:fest-assert-core:2.0M10'
    testCompile 'xmlunit:xmlunit:1.3'
    testCompile 'org.reflections:reflections:0.9.9-RC1'
}

sourceSets {
    main {
        java {
            srcDir 'src/'
        }
        resources {
            srcDir 'src/'
            exclude "**/*.java"
        }
    }
    test {
        java {
            srcDir 'test/'
        }
        resources {
            srcDir 'test/'
            exclude "**/*.java"
        }
    }
    
    sourceSets.test.runtimeClasspath += files(sourceSets.main.java.srcDirs)
    sourceSets.test.runtimeClasspath += files(sourceSets.test.java.srcDirs)
}


test {
    useJUnit()
    include "**/JUnitTestSuite.*"
    include "**/GWTTestCaseSuite.*"
    include "**/MainFlowProcessorTest.*"

    forkEvery = 1
    maxHeapSize = '512M'
    jvmArgs( ['-XX:MaxPermSize=256M', '-Xss16M']  )
}

class GWTCompile extends DefaultTask {
    def String buildDir;
    def String module;

    @TaskAction
    public void exec() {
        def javaConvention = project.getConvention().getPlugin(JavaPluginConvention);
        def mainSourceSet = javaConvention.getSourceSets().getByName(SourceSet.MAIN_SOURCE_SET_NAME);
        project.file(buildDir).mkdirs()

        project.javaexec {
            main = 'com.google.gwt.dev.Compiler'
            classpath {
                [
                    mainSourceSet.java.srcDirs,           // Java source
                    mainSourceSet.output.resourcesDir,    // Generated resources
                    mainSourceSet.output.classesDir,      // Generated classes
                    mainSourceSet.compileClasspath,       // Deps
                ]   
            }
         
            args =
                [
                    this.module, // Your GWT module
                    '-war', this.buildDir,
                    '-logLevel', 'INFO',
                    '-localWorkers', '2',
                    '-XdisableCastChecking'
                ]

            systemProperty 'gwt.args', '-logLevel WARN'
            systemProperty 'java.awt.headless', 'true'

            maxHeapSize = '512M'
            jvmArgs( '-XX:MaxPermSize=256M' )
        }
    }
}

task compileMain(dependsOn: [classes], type: GWTCompile) {
    module = 'eu.ydp.empiria.player.Player'
    buildDir = "${project.buildDir}/gwt"
}
