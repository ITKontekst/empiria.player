buildscript {
    repositories {
        ivy {
            url "http://dev/ivyrep/public/"
            layout 'pattern', {
                artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
                ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            }
        }
    }

    dependencies {
        classpath group: 'eu.ydp', name: 'gradleGwtCompilerPlugin', version: 'latest.integration'
    }
}

import eu.ydp.gradle.gwt.GWTCompileTask

def empiriaWar = file("war")
def empiriaBuildDir = new File(empiriaWar, "empiria.player")
def empiriaWebkitWar = file("build/gwt")
def empiriaWebkitBuildDir = new File(empiriaWebkitWar, "empiria.player")
def versionFile = file("${projectDir}/src/main/java/eu/ydp/empiria/player/client/version/version.txt")
def buildProfile = project.getProperties().get('buildProfile', 'develop')

println "Picked build profile [$buildProfile]"

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'sonar-runner'
apply from: "gradle/profiles/${buildProfile}.gradle"
apply plugin: 'ivy-publish'
apply plugin: "groovy"

sourceCompatibility = 1.7
compileJava.options.encoding = 'UTF-8'

ext {
    tstamp = new Date().format("yyyyMMddHHmmssSSS")
    buildnumber = "${System.env["BUILD_NUMBER"] ?: "${tstamp}"}"
    jacocoReport = "${buildDir}/jacoco/testJUnit.exec"
}

repositories {
    maven {
        url "https://jitpack.io"
    }
    jcenter {
        url "http://jcenter.bintray.com/"
    }
    ivy {
        url "http://dev/ivyrep/public/"
        layout 'pattern', {
            artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
        }
    }
    ivy {
        url "http://dev/ivyrep/shared/"
        layout 'pattern', {
            artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
        }
    }
    mavenCentral()
    maven {
        url 'https://code.lds.org/nexus/content/repositories/thirdparty/'
    }
}

configurations.all {
    resolutionStrategy {
        force group: 'eu.ydp', name: 'ydpgwtutils', version: ydp_gwt_utils_ver
    }
}

dependencies {
    compile group: 'com.google.gwt', name: 'gwt-servlet', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-user', version: '2.7.0'
    compile group: 'com.google.gwt', name: 'gwt-dev', version: '2.7.0'

    compile group: 'com.google.gwt.inject', name: 'gin', version: '2.1.2'
    compile group: 'com.googlecode.gwtquery', name: 'gwtquery', version: '1.4.2'
    compile group: 'com.googlecode.gwtquery.bundles', name: 'gquery-dnd-bundle', version: '1.0.6'
    compile group: 'com.google.guava', name: 'guava-gwt', version: '18.0'

    compile group: 'eu.ydp', name: 'mathPlayer', version: mathplayer_ver
    compile group: 'eu.ydp', name: 'jsfilerequest', version: jsfilerequest_ver
    compile group: 'eu.ydp', name: 'gwtFlashMedia', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'gwtCreateJs', version: gwt_createjs_ver
    compile group: 'eu.ydp', name: 'jaxb4gwt', version: 'latest.integration'
    compile group: 'eu.ydp', name: 'matheclipse-parser-gwt', version: '0.0.10+'

    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.10.19'
    testCompile group: 'com.google.gwt.gwtmockito', name: 'gwtmockito', version: '1.1.5'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library', version: '1.3'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.0.4'
    testCompile group: 'org.easytesting', name: 'fest-assert-core', version: '2.0M10'
    testCompile group: 'xmlunit', name: 'xmlunit', version: '1.3'
    testCompile group: 'org.reflections', name: 'reflections', version: '0.9.9-RC1'
    testCompile group: 'org.spockframework', name: 'spock-core', version: '1.0-groovy-2.3'

    testCompile group: 'org.objenesis', name: 'objenesis', version: '2.1'
    testCompile group: 'com.github.cglib.cglib', name: 'cglib-nodep', version: '5503bcca74'
}

eclipse {
    project {
        natures 'com.google.gwt.eclipse.core.gwtNature', 'org.springsource.ide.eclipse.gradle.core.nature', 'org.eclipse.jdt.groovy.core.groovyNature'
        buildCommand 'com.google.gdt.eclipse.core.webAppProjectValidator'
        buildCommand 'com.google.gwt.eclipse.core.gwtProjectValidator'
    }
    classpath {
        containers 'com.google.gwt.eclipse.core.GWT_CONTAINER'

        defaultOutputDir file('/war/WEB-INF/classes')
    }
}

sonarRunner {
    sonarProperties {
        property "sonar.host.url", sonar_host_url
        property "sonar.jdbc.url", sonar_jdbc_url
        property "sonar.jdbc.driverClassName", sonar_jdbc_driverClassName
        property "sonar.jdbc.username", sonar_jdbc_username
        property "sonar.jdbc.password", sonar_jdbc_password
        property "sonar.projectName", sonar_project_name
        property "sonar.core.codeCoveragePlugin", "jacoco"
    }
}

sourceSets {
    test.java.srcDirs += 'src/test/gwt'
    test.runtimeClasspath += files(main.java.srcDirs)
    test.runtimeClasspath += files(test.java.srcDirs)
    test.runtimeClasspath += files(test.groovy.srcDirs)
}

task testJUnit(type: Test) {
    jacoco {
        enabled = true
    }
    useJUnit()
    include "**/*Spec.class"
    include "**/*Test.class"


    maxHeapSize = '1024M'
    jvmArgs(['-XX:MaxPermSize=512M', '-Xss2M'])
    reports.html.destination = file("$buildDir/reports/junit")
}
task testGWTUnit(type: Test) {
    jacoco {
        enabled = false
    }
    useJUnit()
    include "**/GWTTestCaseSuite.*"
    include "**/MainFlowProcessorGWTTestCase.*"
    include "**/PageSwitchAnimationGWTTestCase.*"

    forkEvery = 1
    maxHeapSize = '1024M'
    jvmArgs(['-XX:MaxPermSize=512M', '-Xss2M'])
    reports.html.destination = file("$buildDir/reports/gwt")
}
testGWTUnit.shouldRunAfter testJUnit

task test(overwrite: true, dependsOn: [testJUnit, testGWTUnit]) {
    ext {
        jacoco = [destinationFile: file(jacocoReport)]
    }
}

jacoco {
    toolVersion = '0.7.1.201405082137'
}

jacocoTestReport {
    executionData = files(jacocoReport)
    reports {
        xml.enabled true
        csv.enabled false
    }
}
testJUnit.finalizedBy jacocoTestReport

task cleanCache(dependsOn: clean) {
    delete "gwt-unitCache/"
}

task compileMain(dependsOn: [classes], type: GWTCompileTask) {
    module = 'eu.ydp.empiria.player.Player'
    buildDir = empiriaWar
    gwtArgs['-XjsInteropMode'] = 'JS'
}

task compileDev(dependsOn: [classes], type: GWTCompileTask) {
    module = 'eu.ydp.empiria.player.Player_dev'
    buildDir = empiriaWar
    gwtArgs['-XjsInteropMode'] = 'JS'
}

task compileDevPretty(dependsOn: [classes], type: GWTCompileTask) {
    module = 'eu.ydp.empiria.player.Player_dev'
    buildDir = empiriaWar
    gwtArgs['-XjsInteropMode'] = 'JS'
    gwtArgs['-style'] = 'PRETTY'
}

task compileWebkit(dependsOn: [classes], type: GWTCompileTask) {
    module = 'eu.ydp.empiria.player.Player_webkit'
    buildDir = empiriaWebkitWar
    gwtArgs['-XjsInteropMode'] = 'JS'
}


def configCopyMetaInfoAndZip(playerDir, targetZip) {
    return {
        def metadataProperties = [version: project.ext.version, tstamp: tstamp, buildnumber: buildnumber]

        archiveName targetZip
        destinationDir file("dist")
        from playerDir
        from(file("metadata.template.xml")) {
            rename "metadata.template.xml", "metadata.xml"
            expand metadataProperties
        }
    }
}

task createVersion() {
    versionFile.createNewFile()
    versionFile.write "${project.ext.version}-${buildnumber}"
}

task zipMain(dependsOn: [compileMain], type: Zip) {
    configure configCopyMetaInfoAndZip(empiriaBuildDir, "empiriaplayer.zip")
}
task zipWebkit(dependsOn: [compileWebkit], type: Zip) {
    configure configCopyMetaInfoAndZip(empiriaWebkitBuildDir, "empiriaplayer_webkit.zip")
}

task compileAll(dependsOn: [compileMain, compileWebkit])

task distMain(dependsOn: [cleanCache, createVersion, test, zipMain])
task distWebkit(dependsOn: [cleanCache, createVersion, test, zipWebkit])

task dist(dependsOn: [distMain, distWebkit])

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ["-implicit:none"]
    options.compilerArgs += ["-sourcepath", ""]
}

publishing {
    publications {
        ivy(IvyPublication) {
            organisation 'eu.ydp'
            module 'empiriaPlayer'
            revision "${project.ext.version}-${buildnumber}"
            descriptor.status = project.ext.status
            configurations {
                create("default")
                create("webkit")
            }
            artifacts {
                artifact("${projectDir}${File.separator}dist${File.separator}empiriaplayer.zip") {
                    name 'empiriaplayer'
                    classifier "source"
                    extension "zip"
                    conf "default"
                    builtBy distMain
                }

                artifact("${projectDir}${File.separator}dist${File.separator}empiriaplayer_webkit.zip") {
                    name 'empiriaplayer_webkit'
                    classifier "source"
                    extension "zip"
                    conf "webkit"
                    builtBy distWebkit
                }
            }
        }
    }
    repositories {
        ivy {
            url 'http://dev/ivyrep/public'
            layout 'pattern', {
                artifact '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
                ivy '[organisation]/[module]/[revision]/[type]s/[artifact].[ext]'
            }
        }
    }
}
